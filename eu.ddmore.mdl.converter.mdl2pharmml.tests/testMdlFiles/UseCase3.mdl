warfarin_PKPD_turnover_dat = dataObj{
     DECLARED_VARIABLES{CP_obs::observation; PCA_obs::observation; GUT::dosingTarget }

     DATA_INPUT_VARIABLES{
      ID : 	 { use is id }
      TIME : { use is idv }
      WT : 	 { use is covariate }
      logtWT : 	 { use is covariate }
      AGE :  { use is covariate }
      SEX :  { use is catCov withCategories{female when 0, male when 1} }
      AMT :  { use is amt, variable = GUT }
      DVID : { use is dvid }
      DV :   { use is dv, define={1 in DVID as CP_obs, 2 in DVID as PCA_obs} }
      MDV :  { use is mdv }
   }# end HEADER

   SOURCE{
      srcfile : {file="warfarin_conc_pca.csv", 
      			 inputFormat is nonmemFormat}
   }# end FILE
} # end data object

warfarin_PKPD_turnover_par = parObj{
   STRUCTURAL{
# The STRUCTURAL block define the structural model parameters.
      #THETA
      POP_CL : {value=0.134,lo=0.01,hi=1} # L/h/70kg
      POP_V : {value=8.1,lo=0.01,hi=20} # L/70kg
      POP_TABS : {value=0.575,lo=0.01,hi=24} # h
      POP_TLAG : {value=0.815,lo=0.01,hi=24} # h
      POP_S0 : {value=96.7,lo=0.01,hi=200}
      POP_EMAX : {value=-1.,fix=true}
      POP_C50 : {value=1.2,lo=0.01,hi=10}
      POP_TEQ : {value=12.9,lo=0.01,hi=100}
	  BETA_CL_WT : { value = 0.75, fix = true }
	  BETA_V_WT : { value = 1, fix = true }
   }# end STRUCTURAL

   VARIABILITY{
      #OMEGA
      PPV_CL : {value=0.0638 ,type is var}
      PPV_V : {value=0.0168 ,type is var}
      PPV_TABS : {value=0.691 ,type is var}
      PPV_LAG : {value=0.155 ,type is var}
      PPV_S0 : {value=0.00377 ,type is var}
      PPV_EMAX : {value=0 ,type is var,fix=true}
      PPV_C50 : {value=0.21 ,type is var}
      PPV_TEQ : {value=0.0165 ,type is var}
      #SIGMA
      RUV_PROP : {value=0.0133 ,type is var}
      RUV_ADD : {value=0.05 ,type is var} # mg/L
      RUV_FX : {value=15.3 ,type is var}
   }# end VARIABILITY

} # end of parameter object

warfarin_PKPD_turnover_mdl = mdlObj{
	IDV { T }

	VARIABILITY_LEVELS{
		ID : { level=2, type is parameter }
		DV : { level=1, type is observation }	
	}

	COVARIATES(type is idvDependent){
		WT
	}

	COVARIATES(type is constant){
		logtWT
	}
	
# The STRUCTURAL_PARAMETERS Block defines the structural model parameters.
   STRUCTURAL_PARAMETERS{
# A name for each parameter is required. Other attributes such as units are optional.
# Units may be used to achieve consistency between the parameter, data and model object values.
# This could be implemented during the WP2 translation process.

      #THETA
      POP_CL
      POP_V
      POP_TABS
      POP_TLAG
      POP_S0
      POP_EMAX
      POP_C50
      POP_TEQ
      BETA_CL_WT
      BETA_V_WT
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
# The VARIABILITY_PARAMETERS Block defines the variability parameters.
      #OMEGA
      PPV_CL
      PPV_V
      PPV_TABS
      PPV_LAG
      PPV_S0
      PPV_EMAX
      PPV_C50
      PPV_TEQ
      #SIGMA
      RUV_PROP
      RUV_ADD
      RUV_FX
   }# end VARIABILITY_PARAMETERS

	GROUP_VARIABLES{
		GRP_CL = BETA_CL_WT * WT
	}

   RANDOM_VARIABLE_DEFINITION(level=ID){
# The RANDOM_VARIABLE_DEFINITION block is used to define the distribution of random variables.
      ETA_CL ~ Normal( mean=0, var=PPV_CL) 
      ETA_V ~ Normal( mean=0, var=PPV_V)
      ETA_TABS ~ Normal( mean=0, var=PPV_TABS)
      ETA_TLAG ~ Normal( mean=0, var=PPV_LAG)
      ETA_S0 ~ Normal( mean=0, var=PPV_S0)
      ETA_EMAX ~ Normal( mean=0, var=PPV_EMAX)
      ETA_C50 ~ Normal( mean=0, var=PPV_C50)
      ETA_TEQ ~ Normal( mean=0, var=PPV_TEQ)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
# The INDIVIDUAL_VARIABLES block is used to express the individual variables and their distribution.
# If this is not a population model then this block is not required.

	 CL : { type is general,  trans is ln, grp = GRP_CL, ranEff = [ETA_CL]  }
	 V : { type is linear,  trans is ln, pop = POP_V, fixEff = [{coeff=BETA_V_WT, cov=logtWT}] , ranEff = [ETA_V]  }
	 TABS : { type is linear,  trans is ln, pop = POP_TABS, ranEff = [ETA_TABS]  }
	 TLAG : { type is linear,  trans is ln, pop = POP_TLAG, ranEff = [ETA_TLAG]  }
	  S0 : { type is linear,  trans is ln, pop = POP_S0, ranEff = [ETA_S0]  }
	  EMAX : { type is linear,  trans is ln, pop = POP_EMAX, ranEff = [ETA_EMAX]  }
	  C50 : { type is linear,  trans is ln, pop = POP_C50, ranEff = [ETA_C50]  }
	  TEQ : { type is linear,  trans is ln, pop = POP_TEQ, ranEff = [ETA_TEQ]  }
#      if (EMAX<-1) nul=errorexit(1,101) 
      KA=2/TABS
      S2=V
      KPCA=2/TEQ
      RPCA=S0*KPCA
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
   DEQ{
      RATEIN= if(T >= TLAG) then KA*GUT
			  else 0
	  GUT : { deriv =(- RATEIN), init = 0, x0 = 0 }
	  CENTRAL : { deriv =(RATEIN - CL * CENTRAL / V), init = 0, x0 = 0 } 
      CC=CENTRAL/V

      DPCA=PCA
      PD=1+EMAX*CC/(C50+CC)			
      PCA : {deriv=RPCA*PD - KPCA*DPCA, init = S0}
   	  }# end ODE
   }# end MODEL_PREDICTION

	RANDOM_VARIABLE_DEFINITION(level=DV){
      EPS_CP ~ Normal( mean=0, var=1)
      EPS_PCA ~ Normal( mean=0, var=1)
	}

   OBSERVATION{
   	 CP_obs : { type is combinedError1,  additive = RUV_ADD, proportional = RUV_PROP, eps = EPS_CP, prediction = CC  }
   	 PCA_obs : { type is additiveError,  additive = RUV_FX, eps = EPS_PCA, prediction = PCA  }
   }# end OBSERVATION
} # end of model object

warfarin_PKPD_turnover_task = taskObj {
ESTIMATE{ 
		set algo is saem 
}
} # end of task object

warfarin_PK_ODE_mog = mogObj {
	INFO{
		set problemStmt = "Warfarin estimation Use Case."
	}
    OBJECTS{
		warfarin_PKPD_turnover_dat : { type is dataObj }
		warfarin_PKPD_turnover_mdl : { type is mdlObj }
		warfarin_PKPD_turnover_par : { type is parObj }
		warfarin_PKPD_turnover_task : { type is taskObj }
	}
}
