# Model based on the Hansson et al. CPT:PsP 2013
# PKPD modeling of VEGF, sVEGFR-2, sVEGFR-3 and sKIT following sunitinib treatment in GIST


Hansson2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      CYCL:{type=categorical}
      TIME:{type=continuous,units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical} # 1=Dose ; 6=sVEGFR2 
      DVX:{type=continuous, units="mg/L"}
      DV:{type=continuous,units="mg/L"} # log transform data
      DOS:{type=continuous,units="mg"}
      PLA:{type=categorical} # placebo=1, treated=0
      CL:{type=continuous,units="L/h"}
      EVID:{type=categorical}
   }
   
   SOURCE{
       file="BIOMARKER_exDATA_sVEGFR2.csv"
       inputformat=nonmemFormat
       ignore="#"
   }
}

Hansson2013_par = parobj{

   STRUCTURAL{

      POP_IMAX:{value=1, fix=true} 
      POP_IC50:{value=1, lo=0, units="mg/L*h"} 

     #sVEGFR-2
      POP_BM02:{value=8670, lo=0, units="pg/mL"} # baseline sVEGFR-2
      POP_MRT2:{value=554, lo=0, units="h"} 
      POP_HILL2:{value=1.54, lo=0}
   
      #Residual Error
      POP_RES_sVEGFR2_PROP:{value=0.12, lo=0, units="pg/mL"} 
      POP_RES_sVEGFR2_ADD:{value=583, lo=0}
   }

   VARIABILITY{
      OMEGA_BM02:{value=0.0369,type=VAR}
      OMEGA_MRT_VEGFs:{value=0.060, type=VAR}
      OMEGA_IC502 : { value=0.189, type=VAR }
               
      SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
   }
}

### Model object
Hansson2013_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{type=categorical, use=id,level=2}
      CYCL:{type=categorical}
      TIME:{type=continuous, use=idv, units="h"}
      DAYS:{type=continuous,units="DAYS"}
      DVX:{type=continuous, units="mg/L"} 
      DV:{type=continuous,units="mg/L",use=dv, prediction=sVEGFR2_obs, level=1}
      DOS:{type=continuous,units="mg"}
      CL:{type=continuous,units="L/h", use=covariate}
      EVID:{type=categorical, use=evid}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 # Common IC50 for the four biomarkers which were found to be highly correlated
      POP_BM02
      POP_MRT2 
      POP_HILL2
      
      #Residual Error
      POP_RES_sVEGFR2_ADD 
      POP_RES_sVEGFR2_PROP
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM02
      OMEGA_MRT_VEGFs
      OMEGA_IC502

      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_BM02 ~ {type=normal, mean=0, var=OMEGA_BM02, level=ID}
      eta_MRT_VEGFs ~ {type=normal, mean=0, var=OMEGA_MRT_VEGFs, level=ID}
      eta_IC502 ~ {type=normal, mean=0, var=OMEGA_IC502, level=ID}
}# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{ 
      BM02 : {type=linear, trans=log, pop = POP_BM02, ranEff=eta_BM02} 
      IMAX2 = POP_IMAX
      IC502 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC502} 
      HILL2 = POP_HILL2     
      MRT2 : {type=linear, trans=log, pop = POP_MRT2, ranEff=eta_MRT_VEGFs} 
         
      # Derived parameters
      KOUT2 = 1/MRT2
      KIN2 = BM02*KOUT2     
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL
      
       ODE{
       EFF2 = IMAX2*AUC^HILL2/(IC502^HILL2+AUC^HILL2) # VEGFR2: sigmoid Imax drug effect relationship
       sVEGFR2 : {deriv=KIN2*(1-EFF2)-KOUT2*sVEGFR2, init=BM02, wrt=T}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
      eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
                           
     sVEGFR2_obs : {type=continuous, error=combinedError2log(additive=POP_RES_sVEGFR2_ADD, proportional=POP_RES_sVEGFR2_PROP,f=sVEGFR2),
       prediction=sVEGFR2, eps=eps_RES_W}
       
   
   }# end OBSERVATION

} # end of model object
       
       
Hansson2013_task = taskobj{
### Task Properties object

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCEI"] 
	}
}# end of task object

Hansson2013 = mog{
	Hansson2013_data
	Hansson2013_par
	Hansson2013_mdl
	Hansson2013_task
}