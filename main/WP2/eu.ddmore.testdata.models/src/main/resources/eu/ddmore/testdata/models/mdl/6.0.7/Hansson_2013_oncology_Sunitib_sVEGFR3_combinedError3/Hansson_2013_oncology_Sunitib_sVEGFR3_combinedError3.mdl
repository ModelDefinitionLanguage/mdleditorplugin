# Model based on the Hansson et al. CPT:PsP 2013
# PKPD modeling of VEGF, sVEGFR-2, sVEGFR-3 and sKIT following sunitinib treatment in GIST


Hansson2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      CYCL:{type=categorical}
      TIME:{type=continuous,units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical} # 1=Dose ; ; 7=sVEGFR3 
      DVX:{type=continuous, units="mg/L"}
      DV:{type=continuous,units="mg/L"} # log transform data
      DOS:{type=continuous,units="mg"}
      PLA:{type=categorical} # placebo=1, treated=0
      CL:{type=continuous,units="L/h"}
      EVID:{type=categorical}
   }
   
   SOURCE{
       file="BIOMARKER_exDATA_sVEGFR3.csv"
       inputformat=nonmemFormat
       ignore="#"
   }
}

Hansson2013_par = parobj{

   STRUCTURAL{

      POP_IMAX:{value=1, fix=true} 
      POP_IC50:{value=1, lo=0, units="mg/L*h"} 

      #sVEGFR-3
      POP_BM03:{value=63900, lo=0, units="pg/mL"} # baseline sVEGFR-3
      POP_MRT3:{value=401, lo=0, units="h"} 

      #Residual Error
      POP_RES_sVEGFR3_ADD:{value=0.22, lo=0, units="pg/mL"} 
      POP_RES_sVEGFR3_PROP:{value=0, fix=true}	  
   }

   VARIABILITY{
      OMEGA_BM03:{value=0.186,type=VAR}
      OMEGA_MRT_VEGFs:{value=0.060, type=VAR}
      OMEGA_IC503 : { value=0.398, type=VAR }

      SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
   }
}

### Model object
Hansson2013_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{type=categorical, use=id,level=2}
      CYCL:{type=categorical}
      TIME:{type=continuous, use=idv, units="h"}
      DAYS:{type=continuous,units="DAYS"}
      DVX:{type=continuous, units="mg/L"} 
      DV:{type=continuous,units="mg/L",use=dv, prediction=sVEGFR3_obs, level=1}
      DOS:{type=continuous,units="mg", use=amt}
      CL:{type=continuous,units="L/h", use=covariate}
      EVID:{type=categorical, use=evid}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 
      POP_BM03
      POP_MRT3 

      #Residual Error
      POP_RES_sVEGFR3_ADD
      POP_RES_sVEGFR3_PROP  	  
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM03
      OMEGA_MRT_VEGFs
      OMEGA_IC503

      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_BM03 ~ {type=normal, mean=0, var=OMEGA_BM03, level=ID}
      eta_MRT_VEGFs ~ {type=normal, mean=0, var=OMEGA_MRT_VEGFs, level=ID}
      eta_IC503 ~ {type=normal, mean=0, var=OMEGA_IC503, level=ID}
}# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      BM03 : {type=linear, trans=log, pop = POP_BM03, ranEff=eta_BM03}
      IMAX3 = POP_IMAX
      IMAXS = POP_IMAX   
      IC503 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC503}
      MRT3 : {type=linear, trans=log, pop = POP_MRT3, ranEff=eta_MRT_VEGFs}
         
      # Derived parameters
      KOUT3 = 1/MRT3
      KIN3 = BM03*KOUT3     
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL

       ODE{
       EFF3 = IMAX3*AUC/(IC503+AUC)                      # V EGFR3: Imax drug effect relationship
       sVEGFR3 : {deriv=KIN3*(1-EFF3)-KOUT3*sVEGFR3, init=BM03, wrt=T}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
      eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
                      
      sVEGFR3_obs : {type=continuous, error=combinedError3(additive=POP_RES_sVEGFR3_ADD, proportional=POP_RES_sVEGFR3_PROP,f=sVEGFR3),
       prediction=sVEGFR3, eps=eps_RES_W}
 	  
  	  }# end OBSERVATION

} # end of model object
       
       
Hansson2013_task = taskobj{
### Task Properties object

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCE"] 
	}
}# end of task object

Hansson2013 = mog{
	Hansson2013_data
	Hansson2013_par
	Hansson2013_mdl
	Hansson2013_task
}