# Model based on the Hansson et al. CPT:PsP 2013
# PKPD modeling of sKIT following sunitinib treatment in GIST


Hansson2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      CYCL:{type=categorical}
      TIME:{type=continuous,units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical} # 1=Dose ; 8=SKIT
      DVX:{type=continuous, units="mg/L"}
      DV:{type=continuous,units="mg/L"} # log transform data
      DOS:{type=continuous,units="mg"}
      PLA:{type=categorical} # placebo=1, treated=0
      CL:{type=continuous,units="L/h"}
      EVID:{type=categorical}
   }
   
   SOURCE{
       file="BIOMARKER_exDATA_sKIT.csv"
       inputformat=nonmemFormat
       ignore="#"
   }
}

Hansson2013_par = parobj{

   STRUCTURAL{

      POP_IMAX:{value=1, fix=true} 
      POP_IC50:{value=1, lo=0, units="mg/L*h"} 

      #sKIT
      POP_BM0S:{value=39200, lo=0, units="pg/mL"} # baseline sKIT
      POP_MRTS:{value=2430, lo=0, units="h"}
      POP_TVSLP:{value=0.035, lo=-0.06, units="1/h"}
            
      #Residual Error
      POP_RES_sKIT_ADD:{value=0, fix=true} 
      POP_RES_sKIT_PROP:{value=0.224, lo=0, units="pg/mL"}    
   }

   VARIABILITY{
      OMEGA_BM0S:{value=0.254,type=VAR}
      OMEGA_MRT_sKIT:{value=0.0753, type=VAR}
      OMEGA_IC50S : { value=5.77, type=VAR }
      OMEGA_TVSLPS:{value=3.01, type=VAR}     
          
      SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
   }
}

### Model object
Hansson2013_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{type=categorical, use=id,level=2}
      CYCL:{type=categorical}
      TIME:{type=continuous, use=idv, units="h"}
      DAYS:{type=continuous,units="DAYS"}
      DVX:{type=continuous, units="mg/L"} 
      DV:{type=continuous,units="mg/L",use=dv, prediction=sKIT_obs, level=1}
      DOS:{type=continuous,units="mg"}
      CL:{type=continuous,units="L/h", use=covariate}
      EVID:{type=categorical, use=evid}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 
      POP_BM0S
      POP_MRTS
      POP_TVSLP
      
      #Residual Error
      POP_RES_sKIT_ADD        
      POP_RES_sKIT_PROP 
         }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM0S
      OMEGA_MRT_sKIT
      OMEGA_IC50S
      OMEGA_TVSLPS     
      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_BM0S ~ {type=normal, mean=0, var=OMEGA_BM0S, level=ID}
      eta_MRT_sKIT ~ {type=normal, mean=0, var=OMEGA_MRT_sKIT, level=ID}
      eta_IC50S ~ {type=normal, mean=0, var=OMEGA_IC50S, level=ID}
      eta_TVSLPS ~ { type=normal, mean=0, var=OMEGA_TVSLPS, level=ID}   
}# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      BM0S : {type=linear, trans=log, pop = POP_BM0S, ranEff=eta_BM0S} 
      IMAXS = POP_IMAX
      IC50S : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC50S} 
      MRTS : {type=linear, trans=log, pop = POP_MRTS, ranEff=eta_MRT_sKIT} 

      TVSLPS = POP_TVSLP/1000
      DPSLPS : {type=linear, trans=log, pop = TVSLPS, ranEff=eta_TVSLPS} 
         
      # Derived parameters
      KOUTS = 1/MRTS    
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL

      DPS = BM0S*(1+DPSLPS*TIME)      # Linear disease progression model for SKIT
      KINS = DPS*KOUTS
       
       ODE{
       EFFS = IMAXS*AUC/(IC50S+AUC)                      # SKIT  : Imax drug effect relationship

       sKIT : {deriv= KINS*(1-EFFS)-KOUTS*sKIT, init=BM0S, wrt=T}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
      eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}

  	  sKIT_obs : {type=continuous, error=combinedError2log(additive=POP_RES_sKIT_ADD, proportional=POP_RES_sKIT_PROP, f=sKIT), prediction=sKIT, eps=eps_RES_W}
  
   }# end OBSERVATION

} # end of model object
       
       
Hansson2013_task = taskobj{
### Task Properties object

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCEI"] 
	}
}# end of task object

Hansson2013 = mog{
	Hansson2013_data
	Hansson2013_par
	Hansson2013_mdl
	Hansson2013_task
}