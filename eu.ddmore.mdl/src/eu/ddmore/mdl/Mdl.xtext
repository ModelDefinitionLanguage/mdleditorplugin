grammar eu.ddmore.mdl.Mdl
generate mdl "http://eu.ddmore.mdl/mdl/Mdl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://eu.ddmore.mdllib/mdllib/MdlLib" as lib

Mcl hidden(WS, ML_COMMENT, SL_COMMENT):
	objects+=MclObject*;

MclObject returns MclObject:
	name=ID '=' objId=[lib::ObjectDefinition] blkArgs=BlockArguments '{' blocks += BlockStatement* '}';


BlockStatement:
	blkId = [lib::BlockDefinition] blkArgs=BlockArguments body=(BlockStatementBody|BlockTextBody)
;

BlockStatementBody returns BlockBody:
	{BlockStatementBody}'{'
		statements+=Statement*
	'}'
;

BlockTextBody returns BlockBody:
	{BlockTextBody}text=TEXT_BLOCK
;

BlockArguments:
	{BlockArguments} ('(' args+=ValuePair (',' args+=ValuePair)* ')')?
;

//BlockArgument:
//	ValuePair
//;


Statement:
	EquationTypeDefinition|ListDefinition|
	RandomVariableDefinition|BlockStatement|AnonymousListStatement|
	EnumerationDefinition|PropertyStatement //|
//	CategoricalProbabilityAssign//|UserFunctionDefinition
;


MdlSymbolDefinition returns lib::SymbolDefinition:
	EquationDefinition | ListDefinition | RandomVariableDefinition | TransformedDefinition|
	EnumerationDefinition
;


FuncUnnamedArgDefns returns lib::UnnamedFuncArgs:
	{lib::UnnamedFuncArgs} arguments+=FuncArgumentDefinition (',' arguments+=FuncArgumentDefinition)*
;

FuncArgumentDefinition returns lib::FuncArgumentDefinition:
	{lib::FuncArgumentDefinition} name=ID typeSpec=TypeSpec 
;

EquationTypeDefinition:
	EquationDefinition|TransformedDefinition
;

EquationDefinition returns EquationTypeDefinition:
	{EquationDefinition} name = ID typeSpec=TypeSpec? ('=' expression = Expression)? ';'?
;

FunctionSpec returns lib::FunctionSpec:
	{lib::FunctionSpec}  '(' argument=FuncUnnamedArgDefns? ')' returnType=TypeSpec 'is' body=Expression
;


TransformedDefinition returns EquationTypeDefinition:
	{TransformedDefinition} transform = [lib::FunctionDefnBody] '(' name=ID ')' '=' expression=Expression ';'?
;

RandomVariableDefinition:
	(name = ID|  transform = ID '(' name=ID ')') '~' distn = Expression ';'?
//	(name = ID|  transform = ID '(' name=ID ')') 
//		(WITH BEGIN catDefns+=CategoryValueDefinitionNoMapping (',' catDefns+=CategoryValueDefinitionNoMapping)* END)? RANDOM_ASSIGN distn = Expression ';'?
;

TypeSpec returns lib::TypeSpec:
	{lib::TypeSpec} '::' =>typeName = [lib::TypeDefinition|ID] ('[' elementType=TypeSpec ']'|'[[' cellType=TypeSpec ']]'| functionSpec=FunctionSpec)?
;

EnumerationDefinition:
	name = ID  catDefn=CategoricalDefinitionExpr ('~' distn = Expression)? ';'?
;

//CategoricalProbabilityAssign:
//	(probFunc=ProbabilityFunction ASSIGN expression=Expression  |
//	transform = ID '(' probFunc=ProbabilityFunction ')' ASSIGN expression=Expression) ';'?
//;

//ProbabilityFunction:
//	probFunc = 'P' '(' catRef=SymbolReference relOp=('=='|'<='|'>='|'<'|'>') catValRef=CategoryValueReference ')'
//;


ListDefinition:
	name = ID ':' list = (AttributeList|ListPiecewiseExpression|ListIfExpression) ';'?
;

AbstractAttributeList:
	AttributeList|ListPiecewiseExpression|ListIfExpression
;

ListPiecewiseExpression:
	{ListPiecewiseExpression} 'piecewise' '{' '{' when+=ListPWClause (=>';' when+=ListPWClause)* (';' 'otherwise' otherwise=AttributeList )?  '}' '}'
;

ListPWClause:
	{ListPWClause} value=AttributeList 'when' cond=Expression
;

ListIfExpression:
	 {ListIfExpression}
	 	ifelseClause+=ListIfClause
	 	(=> ifelseClause+=ListElifClause)*
		(=>elseClause=ListElseClause)?
	;

ListIfExprPart:
	ListIfClause | ListElifClause
;

ListIfClause returns ListIfExprPart:
	{ListIfClause} 'if' '(' cond=Expression ')' => 'then' value=AttributeList
;

ListElifClause returns ListIfExprPart:
	{ListElifClause} 'elseif' '(' cond=Expression ')' => 'then' value=AttributeList
;

ListElseClause:
	{ListElseClause} 'else' value=AttributeList
;



AnonymousListStatement:
	'::' list = AttributeList ';'?
;

PropertyStatement:
	'set' properties += ValuePair (=> ',' properties += ValuePair)* ';'?
;


CategoricalDefinitionExpr:
	{CategoricalDefinitionExpr} 'withCategories' '{' categories += CategoryValueDefinition (=>',' categories += CategoryValueDefinition)* '}'
//		(ASSIGN expression = Expression)?
;


//CategoryValueDefinition:
//	CategoryValueDefinitionNoMapping | CategoryValueDefinitionWithMapping
//;

//CategoryValueDefinitionNoMapping:
//	{CategoryValueDefinitionNoMapping}name = ID
//;


//CategoryValueDefinitionWithMapping:
CategoryValueDefinition:
	{CategoryValueDefinition}name = ID ('when' mappedTo=Expression)?
//	{CategoryValueDefinitionWithMapping}name = ID (WHEN mappedTo=Expression)?
;

AttributeList:
	{AttributeList} '{' attributes += ValuePair (=> ',' attributes += ValuePair)*  '}'
;

ValuePair:
	(AssignPair|EnumPair)
;

AssignPair:
	argumentName = AttributeName assignOp=('='|'~') expression = Expression
	
;

EnumPair:
	argumentName = AttributeName 'is' expression = EnumExpression
	
;

AttributeName:
	ID
;

EnumExpression returns lib::Expression:
	{EnumExpression}enumValue=ID  (catDefn=CategoricalDefinitionExpr)?
;

MappingExpression returns lib::Expression:
	{MappingExpression} ('{'
		(
			attList+=MappingPair (',' attList+=MappingPair)*
		)
	'}')
;

SubListExpression returns lib::Expression:
	{SubListExpression} '{' attributes += ValuePair (=> ',' attributes += ValuePair)*  '}' 
;

CatValRefMappingExpression returns lib::Expression:
	{CatValRefMappingExpression}('{'
		attLists+=CatValRefMapping (',' attLists+=CatValRefMapping)*
	'}')
;

CatValRefMapping:
	catRef=CategoryValueReference 'when' mappedTo=Expression
;

MappingPair:
	leftOperand=OrExpression 'in' srcColumn=SymbolReference  'as' rightOperand=OrExpression
;

Expression returns lib::Expression:
	OrExpression
;

OrExpression returns lib::Expression:
	AndExpression (=>({OrExpression.leftOperand=current} feature='||') rightOperand=AndExpression)*;

//OpOr:
//	'||';

AndExpression returns lib::Expression:
	EqualityExpression (=>({AndExpression.leftOperand=current} feature='&&') rightOperand=EqualityExpression)*;

//OpAnd:
//	'&&';

EqualityExpression returns lib::Expression:
	RelationalExpression (=>({EqualityExpression.leftOperand=current} feature=('==' | '!='))
	rightOperand=RelationalExpression)*;

//OpEquality:
//	'==' | '!=' ;

RelationalExpression returns lib::Expression:
	AdditiveExpression
	(
	 =>({RelationalExpression.leftOperand=current} feature=('>=' | '<=' | '>' | '<')) rightOperand=AdditiveExpression)*;

//OpCompare:
//	'>=' | '<=' | '>' | '<' ;

AdditiveExpression returns lib::Expression:
	MultiplicativeExpression (=>({AdditiveExpression.leftOperand=current} feature=('+' | '-'))
	rightOperand=MultiplicativeExpression)*;

//OpAdd:
//	'+' | '-';

MultiplicativeExpression returns lib::Expression:
	PowerExpression (=>({MultiplicativeExpression.leftOperand=current} feature=('*' | '/' | '%')) rightOperand=PowerExpression)*;

//OpMulti:
//	'*' | '/' | '%';

PowerExpression returns lib::Expression:
	UnaryExpression (=>({PowerExpression.leftOperand=current} feature='^') rightOperand=UnaryExpression)*;

//OpPow:
//	'^';

UnaryExpression returns lib::Expression:
	{UnaryExpression} feature=("!" | "-" | "+") operand=PrimaryExpression
	| PrimaryExpression
	;

//OpUnary:
//	"!" | "-" | "+";

PrimaryExpression returns lib::Expression:
	Literal
	| SymbolReference
	| FunctionReference
	| CategoryValueReference
	| CatValRefMappingExpression
	| MappingExpression
	| IfExpression
	| PiecewiseExpression
	| ParExpression
	| SubListExpression
	;


ParExpression returns lib::Expression:
	{ParExpression} '(' expr=Expression ')'
;

IfExpression returns lib::Expression:
	 {IfExpression}
	 	ifelseClause+=IfClause
	 	(=> ifelseClause+=ElifClause)*
		(=>elseClause=ElseClause)?
	;

PiecewiseExpression returns lib::Expression:
	{PiecewiseExpression} 'piecewise' '{' '{' when+=PWClause (=>';' when+=PWClause)* (';' 'otherwise' otherwise=Expression )?  '}' '}'
;

PWClause:
	value=Expression 'when' cond=Expression
;

IfExprPart:
	IfClause | ElifClause
;

IfClause returns IfExprPart:
	{IfClause} 'if' '(' cond=Expression ')' => 'then' value=Expression
;

ElifClause returns IfExprPart:
	{ElifClause} 'elseif' '(' cond=Expression ')' => 'then' value=Expression
;

ElseClause:
	{ElseClause} 'else' value=Expression
;

Literal returns lib::Expression:
	RealLiteral |
	ConstantLiteral|
	IntegerLiteral |
	StringLiteral |
	BooleanLiteral |
	VectorLiteral |
	MatrixLiteral
;

SymbolReference:
	{SymbolReference} ref=[lib::SymbolDefinition|ID] ('[' indexExpr = IndexSpec ']'|'(' argList=(UnnamedFuncArguments	| NamedFuncArguments)? ')')?
;

FunctionReference:
	{FunctionReference} '&'ref=[lib::SymbolDefinition]
;

IndexSpec:
		{IndexSpec} rowIdx = IndexRange? (',' colIdx = IndexRange?)? 
			;

IndexRange:
		{IndexRange} (
			begin=Expression (':' end=Expression)?
		)
		;

CategoryValueReference:
	{CategoryValueReference} ref=[CategoryValueDefinition|QualifiedName]
;


NamedFuncArguments returns FuncArguments:
	{NamedFuncArguments} arguments += ValuePair (=> ',' arguments += ValuePair)* 
;

UnnamedFuncArguments returns FuncArguments:
	{UnnamedFuncArguments} args+=UnnamedArgument (=>',' args+=UnnamedArgument)* 
;

UnnamedArgument:
	{UnnamedArgument} argument=Expression
;


BooleanLiteral returns lib::Expression:
	{BooleanLiteral} ('false' | isTrue?='true');


RealLiteral returns lib::Expression:
	{RealLiteral} value=FLOAT;

ConstantLiteral returns lib::Expression:
	{ConstantLiteral} value=CONSTANT;

IntegerLiteral returns lib::Expression:
	{IntegerLiteral} value=INTEGER;

StringLiteral returns lib::Expression:
	{StringLiteral} value=STRING;


MatrixLiteral returns lib::Expression:
	{MatrixLiteral} '[[' (rows += MatrixRow (=> ';' rows += MatrixRow)*)? ']]'
;	

MatrixRow returns lib::Expression:
	{MatrixRow} cells += MatrixElement (=> ',' cells += MatrixElement)*
;

MatrixElement returns lib::Expression:
	{MatrixElement} cell=Expression
;

VectorLiteral returns lib::Expression:
	{VectorLiteral} '[' (expressions += VectorElement (=> ',' expressions += VectorElement)*)? ']'
;	

VectorElement returns lib::Expression:
	{VectorElement} element=Expression
;

QualifiedName:
	ID ('.' ID)
;


terminal CONSTANT:
	'inf'|'pi'|'exponentiale'
;

//terminal TYPE_NAME:
//	'::' + ('A'..'Z')('a'..'z'|'A'..'Z'| '0'..'9')+
//;

//terminal SET: 'set';

//terminal IN: 'in';

//terminal IS: 'is';

//terminal ASSIGN: '=';

//terminal RANDOM_ASSIGN : '~';

//terminal LIST_ASSIGN : ':';

//terminal ANONYMOUS_PREFIX : '::';

//terminal AS: 'as';

//terminal IF: 'if';

//terminal THEN: 'then';

//terminal ELSEIF: 'elseif';

//terminal ELSE: 'else';

//terminal WITH: 'withCategories';

//terminal WHEN: 'when';

//terminal TRUE: 'true';

//terminal FALSE: 'false';

//terminal ORDERED: 'ordered';

terminal STRING:
	'"' (ESCAPE_SEQUENCE | !('\\' | '"'))* '"';

terminal TEXT_BLOCK:
	'<<' -> '>>';

terminal INTEGER returns ecore::EInt:
	('0' | '1'..'9' '0'..'9'*);
	
terminal FLOAT returns ecore::EDouble: 
	'0'..'9' + '.' '0'..'9'* EXPONENT?
    | '.' '0'..'9'+ EXPONENT?
    | '0'..'9'+ EXPONENT
    | ('0'..'9')
    ;

terminal fragment SIGN:
	('+'|'-')
;

terminal fragment ESCAPE_SEQUENCE:
	'\\' ('b' | 't' | 'n' | 'f' | 'r' | '\"' | '\'' | '\\')
	| UNICODE_ESCAPE
	| OCTAL_ESCAPE;

terminal fragment OCTAL_ESCAPE:
	  '\\' ('0'..'3') ('0'..'7') ('0'..'7')
	| '\\' ('0'..'7') ('0'..'7')
	| '\\' ('0'..'7');

terminal fragment UNICODE_ESCAPE:
	'\\' 'u' HEXDIGIT HEXDIGIT HEXDIGIT HEXDIGIT;

terminal fragment HEXDIGIT:
	('0'..'9' | 'a'..'f' | 'A'..'F');

terminal fragment EXPONENT
	: ('e'|'E'|'d'|'D') ('+'|'-')?('0'..'9')+;

terminal SL_COMMENT : ('#') !('\n'|'\r')* ('\r'? '\n')? ;

terminal ML_COMMENT : '/#' -> '#/';

//terminal BEGIN: '{';

//terminal END: '}' ;

terminal ID: ('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'| '0'..'9')*;

terminal WS: (' '|'\t'|'\r'|'\n')+; 
